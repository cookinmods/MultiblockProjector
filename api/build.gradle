plugins {
    id('java')
}

group = 'com.multiblockprojector'
version = rootProject.version
base.archivesName = 'projector-api'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of("${java_version}"))
    }
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
    maven {
        name = 'Mojang'
        url = 'https://libraries.minecraft.net'
    }
}

// Use the NeoForge moddev artifacts produced by the root project
var neoForgeArtifactsDir = rootProject.layout.buildDirectory.dir("moddev/artifacts")

dependencies {
    compileOnly files(neoForgeArtifactsDir.map { dir ->
        dir.asFile.listFiles()?.findAll { it.name.endsWith('.jar') } ?: []
    })
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    compileOnly 'com.mojang:brigadier:1.3.10'
}

// Ensure the NeoForge artifacts are created before we compile
tasks.named('compileJava') {
    dependsOn rootProject.tasks.named('createMinecraftArtifacts')
}
